*******************************************
.param pi = 3.14159265358979323846264338327950288419716939937510582097494459230781640628620899862803482534211706798214808651328230664709384460955058223172535940812848111745028410270193852110555964462294895493038196442881097566593344612847564823

*we could go for 10 digits but..........
*go big or go home



*-----------------------------------------------------------------------------
*
* To use a subcircuit, the name must begin with 'X'.  For example:
* X1 1 2 3 4 5 uA741
*
* connections:   non-inverting input
*                |  inverting input
*                |  |  positive power supply
*                |  |  |  negative power supply
*                |  |  |  |  output
*                |  |  |  |  |
.subckt uA741    1  2  3  4  5
*
  c1   11 12 8.661E-12
  c2    6  7 30.00E-12
  dc    5 53 dx
  de   54  5 dx
  dlp  90 91 dx
  dln  92 90 dx
  dp    4  3 dx
  egnd 99  0 poly(2) (3,0) (4,0) 0 .5 .5
  fb    7 99 poly(5) vb vc ve vlp vln 0 10.61E6 -10E6 10E6 10E6 -10E6
  ga    6  0 11 12 188.5E-6
  gcm   0  6 10 99 5.961E-9
  iee  10  4 dc 15.16E-6
  hlim 90  0 vlim 1K
  q1   11  2 13 qx
  q2   12  1 14 qx
  r2    6  9 100.0E3
  rc1   3 11 5.305E3
  rc2   3 12 5.305E3
  re1  13 10 1.836E3
  re2  14 10 1.836E3
  ree  10 99 13.19E6
  ro1   8  5 50
  ro2   7 99 100
  rp    3  4 18.16E3
  vb    9  0 dc 0
  vc    3 53 dc 1
  ve   54  4 dc 1
  vlim  7  8 dc 0
  vlp  91  0 dc 40
  vln   0 92 dc 40
.model dx D(Is=800.0E-18 Rs=1)
.model qx NPN(Is=800.0E-18 Bf=93.75)
.ends

************************************************************************
.options savecurrents


Vcc vcc 0 5
Vee vee 0 -5
Vin in 0 0 ac 1.0 sin(0 10m 1k)


*High pass filter

C1 in in_plus 220n
R1 in_plus 0 1k

*Amplifier
X1 in_plus in_minus vcc vee out_1 uA741
R3 in_minus out_1 100k
R4 in_minus 0 1k

*Low pass filter
R2 out_1 out 1k
C2 out 0 220n



************************************************************************



************************************************************************
.control


set hcopypscolor=1*
set color1=black
set color2=red
set color3=blue
set color4=violet
set color5=rgb:3/8/0
set color6=rgb:4/0/0
set units=degrees

************************************************************************






************************************************************************
*Operating point analysis
op
************************************************************************

echo  "op_TAB"
print Vcc
print Vee
print in
print in_plus
print in_minus
print out_1
print out
echo  "op_END"




************************************************************************
* time analysis
tran 1e-5 1e-2
************************************************************************

hardcopy vo1.ps v(out)





************************************************************************

ac dec 100 10 0.1MEG
* frequency analysis
************************************************************************

echo  "Zin_TAB"

*input impedance in kohm
let in_Impedance =(-v(in)[40]/i(Vin)[40])
print in_Impedance
echo  "Zin_END"


echo  "freq_TAB"
*set Cost = Mu
set Cost = 13447.65204
let cost = $Cost$ 

let voutm = vecmax(vdb(out))
let vinm = vecmax(vdb(in))
let gain = (voutm-vinm)
let cutoff = gain-3

meas ac f1 WHEN vdb(out)=cutoff RISE=1
meas ac f2 WHEN vdb(out)=cutoff CROSS=LAST
let centralFreq = (f2*f1)^0.5


let gainDev = abs(40-gain)
let FreqDev = abs(1000-centralFreq)

let merit = 1/((gainDev+FreqDev+1e-6)*cost)

print centralFreq
print gain
print gainDev
print FreqDev
print Cost
print merit

echo  "freq_END"


hardcopy vo1f_m.ps (vdb(out)- vdb(in)) cutoff
hardcopy vo1f_ph.ps (vp(out))


************************************************************************

quit
.endc

.end

